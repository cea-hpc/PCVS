#!/bin/nawk -f
#
#  To calculate and plot all the averages 
#  With key: "w", "r"....

BEGIN {
	tmpfile = testname ".avs";
	print "#testname",testname > tmpfile;
	print "#" >> tmpfile;
	print "#proc,bsize,av,SD,max,min,post,pre" >> tmpfile;
	print "#" >> tmpfile;
	gotdate = 0;
}

  (($1 == "start")&&(gotdate == 0)){
  date = $2;
  gotdate = 1;
}
 
  (($1 == "##")&&($2 == "Test")&&($3 == "input")&&($4 == "arguments")){
  testOn = 0;
  fsizeOn = 0;
}

  (($1 == "##")&&($2 == "Test")&&($3 == "output")&&($4 == "information")){
  fsizeOn = 1;
}

  (("testname" == $1)&&(testname == $2)){
  testOn = 1;
}

  ("nprocs" == $1){
  nprocs = $2;
}

  (("blocksize" == $1)&&(fsizeOn == 1)){
  bsize = $2;
  bdsize = bsize*0.000001;
  tnprocs = nprocs;
  for (i=0;i<tnprocs;i++){
    sum[i] = 0.0;
    sums[i] = 0.0;
    Nthing = 0;
    mint[i] = 1000000.0;
    maxt[i] = 0.0;
  }
}

  ((testOn == 1)&&(fsizeOn == 1)&&($1 == key)){
  tnprocs = nprocs;
  Nthing += 1;
  for (i=0;i<tnprocs;i++){
    j = i + 2;
    sum[i] += $j;
    sums[i] += $j*$j;
    if($j > maxt[i]) maxt[i] = $j;
    if($j < mint[i]) mint[i] = $j;
  }
}	

  (("pre_time" == $1)&&(fsizeOn == 1)){
  tnprocs = nprocs;
  for (i=0;i<tnprocs;i++){
    j = i + 2;
    pre_t[i] = $j;
  }
}

  (("post_time" == $1)&&(fsizeOn == 1)){
  tnprocs = nprocs;
  for (i=0;i<tnprocs;i++){
    j = i + 2;
    post_t[i] = $j;
  }
  if(Nthing > 0){
  for (i=0;i<tnprocs;i++){
    sum[i] = sum[i]/Nthing;
    sums[i] = sums[i]/Nthing;
    sums[i] = sums[i] - sum[i]*sum[i];
    sums[i] = sqrt(sums[i]);
  }
  for (i=0;i<tnprocs;i++){
  printf "%d %f %f %f %f %f %f %f\n",i+1,bdsize,sum[i],sums[i],maxt[i],mint[i],post_t[i],pre_t[i] >> tmpfile;
  }
  }
}





