#!/usr/bin/perl

use strict;
use warnings;
#use diagnostics;
use 5.012;
use English qw( -no_match_vars );

###########################################################################
#### major vars
###########################################################################
my %gconf;
my $srcdir;
my $internaldir;
my $buildir;

###########################################################################
#### COMPILATION TIME
###########################################################################
BEGIN {
	$srcdir = `readlink -e \`dirname $0\``; chomp $srcdir;
	$internaldir = $srcdir."/build_scripts";
}

#third-party libs loading
use lib "$srcdir/build_scripts/modules";

# dep inclusions
use PCVS::Helper;
use PCVS::Configuration;
use PCVS::TEngine;
use Data::Dumper; #to help printing hashes (can be removed)
use File::Tee "tee"; # get the output sync'd w/ a file
use File::chdir;
use File::Path qw(mkpath);
use File::Copy::Recursive qw(fcopy dircopy pathempty);
use Getopt::Long;    # parsing options
use Sys::Hostname;

###########################################################################
#### FUNCTIONS
###########################################################################
sub print_summary
{
	my $config_target = "\'default\'";
	$config_target .= " & \'$gconf{'config-target'}\'" if(exists $gconf{'config-target'});
	print "\n >>>>>>>>>>>>>>>>>>>>> GLOBAL INFOS (see \$buildir/config.yml) <<<<<<<<<<<<<<<<<<\n";
	print "      - Run Start date   : ".localtime()."\n";
	print "      - Host name        : ".hostname."\n";
	print "      - Source directory : $gconf{'src'}\n";
	print "      - Build directory  : $gconf{'build'}\n";
	print "      - Loaded Config.   : $config_target\n";
	print "      - Loaded Runtime   : \'$gconf{'runtime'}{'target'}\'\n";
	print "      - Loaded Compiler  : \'$gconf{'compiler'}{'target'}\'\n";
	print "      - Test directories : ".join(", ", @{ $gconf{'select'} } )."\n";
}

sub env_logging
{
	open(my $fh, ">", "$buildir/env.log");
	print $fh "\n#####################\n# GENERAL INFOS \n#####################\n";
	print $fh "Uptime: ".`uptime`;
	print $fh "\n#####################\n# LOADED MODULES \n#####################\n";
	print $fh `module list 2> /dev/null`;
	print $fh "\n#####################\n# ENVIRONMENT VARS \n#####################\n";
	print $fh `env`;
	close($fh);
}

sub trap_signal
{
	print "Stopped by receiving a signal.\n";
	#my @process_list = grep(/[0-9]*/, `ps -o ppid=$$ | sort | uniq`);
	#if(scalar @process_list)
	#{
	#kill('KILL', @process_list);
	#}
	exit(127);
}

sub anonymize_archive
{
	my ($path) = @_;

	my %bad_patterns = ( 
		$srcdir         => "\\\$PCVS_SOURCE_DIR", 
		$buildir        => "\\\$PCVS_BUILD_DIR", 
		$ENV{'HOME'}    => "\\\$PCVS_USER_HOME", 
		$ENV{'LOGNAME'} => "\\\$PCVS_USER_LOGIN",
		(hostname)      => "\\\$PCVS_HOST_NAME"
	);

	foreach my $pat(keys %bad_patterns)
	{
		my $old = $pat;
		my $new = $bad_patterns{$pat};

		# 1. EGREP finds files matching pattern and returns the list of matching files (-l option)
		# 2. perl -pie makes in-place replacement.
		# (Could be replace by a while() loop instead of calling a extern process
		my @matching_list = `egrep -ril \"$old\" $path`; chomp @matching_list;
		if(@matching_list ne 0)
		{
			system("perl -p -i -e 's,$old,$new,g' ".join(" ", @matching_list));
		}
	}

}


sub run_prepare
{
	print "\n >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> PRE-RUN STEP <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<\n";
	#copy the webview in target dir
	print ' * Building the Webview (reachable at $buildir/webview//index.html)'."\n";
	dircopy("$internaldir/generation/jchronoss/tools/webview/*", "$buildir/") or die("Unable to copy the webview: $!");
	my $var = `$buildir/webview_gen_all.sh --skeleton`;
	#copy jsloc
	print " * Saving JsLoc into build directory.\n";
	dircopy("$internaldir/generation/jchronoss/tools/jsLoc/*", "$buildir/") or die("Unable to copy JsLoc: $!");

	#build JCHRONOSS
	print " * Building JCHRONOSS (-j$gconf{j})\n";
	helper_clean_path("$buildir/tmp/build", 0);
	{
		local $CWD = "$buildir/tmp/build"; # equivalent to chdir()
		my $color = "";
		($color = "-DENABLE_COLOR=".($gconf{"color"} == 1 ? "ON" : "OFF")) if (exists $gconf{"color"});
		`cmake $internaldir/generation/jchronoss -DCMAKE_INSTALL_PREFIX=$buildir/tmp $color && make -j$gconf{'j'} install`;
	}
	helper_clean_path("$buildir/tmp/traces", 1);

	env_logging();
}

sub run_finalize
{

	print "\n >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> POST-RUN STEP <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<\n";
	#remove colors sequences
	#copy banners
	dircopy("$internaldir/resources/banners", "$buildir/banners");

	#generate tarball
	unlink("$buildir/last_results.tar.gz");

	#copy the generated webview
	helper_clean_path("$buildir/last_results", 1);
	dircopy("$internaldir/resources/banners", "$buildir/last_results/banners");
	dircopy("$buildir/webview", "$buildir/last_results/webview") or die("Unable to save the webview in the archive !");
	fcopy("$buildir/webview_gen_all.sh", "$buildir/last_results/") or die("Unable to copy webview_gen_all.sh !");
	dircopy("$buildir/jsLoc", "$buildir/last_results/jsLoc") or die("Unable to save JsLoc in the archive !");
	fcopy("$buildir/jsLoc_gen_all.sh", "$buildir/last_results/jsLoc_gen_all.sh") or die("Unable to save JsLoc_gen_all.sh in the archive !");
	fcopy("$buildir/config.yml", "$buildir/last_results/") or die ("Unable to save the configuration file !");
	fcopy("$buildir/config.env", "$buildir/last_results/") or die ("Unable to save the environment file !");
	dircopy("$buildir/tmp/traces", "$buildir/last_results/") or die ("Unable to copy trace files !");

	helper_clean_path("$buildir/last_results/test_suite", 1);
	{
		local $CWD = "$buildir/last_results/test_suite/";
		my @list_files = `find $buildir/test_suite/ -iname 'output*.xml' 2> /dev/null`; chomp @list_files;
		foreach my $res_file(@list_files)
		{
			(my $new_path = $res_file) =~ s@$buildir@$buildir/last_results@;
			my $path = `dirname $new_path`; chomp $path;

			mkpath($path) if (! -d $path);
			fcopy($res_file, $path);
		}
	}

	print " * End Date : ".localtime()."\n";
	fcopy("$buildir/output.log", "$buildir/last_results/") if(!exists $gconf{'log'} || $gconf{'log'});
	fcopy("$buildir/env.log", "$buildir/last_results/") or die ("Unable to save the environment logfile !");

	anonymize_archive("$buildir/last_results");

	print " * Creating the archive (located at $buildir/last_results.tar.gz)\n";
	{
		local $CWD = $buildir;
		`tar -czf $buildir/last_results.tar.gz last_results`;
	}
}

sub run_configure
{
	print "\n >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> BUILD STEP <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<\n";
	my $istart=time();
	engine_init(\%gconf);
	print " * Combinatory System initialized in ".(time()-$istart)." second(s)\n";

	helper_clean_path("$buildir/test_suite", 1);
	print " * Building the list of tests:\n";
	$istart=time();
	foreach my $directory (@{ $gconf{'select'}})
	{
		my $current_path = "$srcdir/$directory";
		my @current_list = `find $current_path -name "pcvs.setup"`; chomp @current_list;
		my $cleaned_by_setup = 0;

		# apply pcvs.setup
		foreach my $setup_elem(@current_list)
		{
			#find the associated subtree
			$setup_elem =~ s/\/pcvs.setup$//;
			(my $name = $setup_elem) =~ s/$srcdir\///g;
			my $current_bpath = "$buildir/test_suite/$name";

			#cleaning
			helper_clean_path($current_bpath, 1);
			$cleaned_by_setup = 1;

			print "    - Setup $name\n";
			my $override_file = `. $buildir/config.env && $setup_elem/pcvs.setup $name`;
			if($override_file ne "")
			{
				open(my $ofh, ">", "$current_bpath/pcvs.yml.in");
				print $ofh  $override_file;
				close($ofh);
			}

		}

		#as pcvs.setup can create some YAML file in work dir, we look for these files in both 
		# source and work directories
		@current_list = `find $current_path $buildir/test_suite/$directory -name "pcvs.yml.in"`; chomp @current_list;
		foreach my $yaml_elem(@current_list)
		{
			# find the test subtree
			$yaml_elem =~ s/\/pcvs.yml.in$//;
			(my $name = $yaml_elem) =~ s/($buildir\/test_suite|$srcdir)\///g;
			my $current_bpath = "$buildir/test_suite/$name";

			#clean if pcvs.setup didn't do it:
			helper_clean_path($current_bpath, 1) if (!$cleaned_by_setup);

			#copy .in file and replace @...@ by real paths (files are in $buildir)
			fcopy("$yaml_elem/pcvs.yml.in", "$current_bpath/pcvs.yml.in") if(! -f "$current_bpath/pcvs.yml.in" );
			system("cat $current_bpath/pcvs.yml.in | sed -e 's,\@SRCPATH\@,$current_path,g' -e 's,\@BUILDPATH\@,$current_bpath,g' > $current_bpath/pcvs.yml");

			#load the YAML desc file
			print "    - Generate $name\n";
			engine_unfold_file($current_bpath, $name, "$current_bpath/pcvs.yml");
		}
	}
	print " * Validation tree built in ".(time()-$istart)." second(s)\n";
	engine_fini();
}

sub prefix_if_exists
{
	my ($prefix, $value) = @_;
	return ($value  ? $prefix.$value : "");
}

sub run_exec
{	
	print "\n >>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>>> RUN STEP <<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<<\n";
	my ($nb_resources, $nb_workers, $compil_w, $run_w, $autokill, $wlist, $blist, $verbosity, $policy, $maxjobt, $max_workert, $min_workert, $rc_on_fail, $test_files) = ();

	$nb_resources = $gconf{'cluster'}{'max_nodes'} || 1;
	$nb_workers = $gconf{'validation'}{'workers'}  || 1;

	$autokill    = prefix_if_exists("--autokill=", $gconf{'validation'}{'autokill'});
	$run_w       = prefix_if_exists("--launcher=$internaldir/launcher/", $gconf{'validation'}{'run_wrapper'});
	$compil_w    = prefix_if_exists("--compil-launcher=$internaldir/launcher/", $gconf{'validation'}{'compil_wrapper'});
	$compil_w    = prefix_if_exists("--compil-launcher=$internaldir/launcher/", $gconf{'validation'}{'run_wrapper'});
	$verbosity   = prefix_if_exists("--verbosity=", $gconf{'verbose'});
	$policy      = prefix_if_exists("--policy=", $gconf{'validation'}{'sched_policy'});
	$maxjobt     = prefix_if_exists("--maxt-job=", $gconf{'validation'}{'job_meantime'});
	$max_workert = prefix_if_exists("--maxt-slave=", $gconf{'validation'}{'worker_maxtime'});
	$min_workert = prefix_if_exists("--mint-slave=", $gconf{'validation'}{'worker_mintime'});
	$rc_on_fail  = ($gconf{'validation'}{'expect-success'}) ? "--expect-success" : "";

	my @list = `find $buildir/test_suite/ -name 'list_of_tests.xml'`; chomp @list;
	$test_files = join(" ", @list);

	my $command = "$buildir/tmp/bin/jchronoss --long-names --nb-resources=$nb_resources --nb-slaves=$nb_workers --build=$buildir/tmp $run_w $compil_w $autokill $verbosity $policy $max_workert $min_workert $rc_on_fail $maxjobt $test_files";

	#print($command."\n");
	system($command);
	my $ret = $?;
	if($ret ne 0 and $rc_on_fail)
	{
		print "Aborting Validation due to failed results (expect-success requirement)\n";
		exit $ret;
	}
}


###########################################################################
#### MAIN
###########################################################################
$SIG{INT} = "trap_signal";

##### DEFAULT VALUES
$gconf{"j"} = 1;
$gconf{'regen'} = 1;
$gconf{'src'} = $srcdir;
$gconf{'build'} = $srcdir."/build";
$gconf{'verbose'} = 0;

GetOptions (
	\%gconf,
	"config-target=s",
	"list-configs|lco",
	"build=s",
	"select=s@",
	"list-directories",
	"compiler-target=s",
	"runtime-target=s",
	"target=s",
	"j:i" ,
	"log!",
	"list-runtimes|lr",
	"list-compilers|lc",
	"list-vars",
	"regen!",
	"color|c!",
	"clean",
	"expect-success",
	"verbose:i",
	"help"
)  or die("Abort due to error(s) while parsing arguments (see --help)!\n");

$_ = $gconf{'build'};
if(/^[^\/].*$/) #check if relative path to prepend it with $CWD
{
	$gconf{'build'}	= "$CWD/".$gconf{'build'};
}
$buildir = $gconf{'build'};

helper_init(\%gconf);
exit(0) if helper_do_not_run_validation();
helper_clean_path($buildir, 0);

my $validation_start = time();

configuration_init(%gconf);
%gconf = configuration_build();
if(exists $gconf{'list-vars'}) { configuration_display_vars();  exit(0); }

tee(STDOUT, '>', "$buildir/output.log") if (!exists $gconf{'log'} || $gconf{'log'});

print `cat $internaldir/resources/banners/test_suite_banner`;

print_summary();

run_prepare();
run_configure() if($gconf{'regen'});
run_exec();
run_finalize();

my ($d, $h, $m, $s) = helper_convert_time(time()-$validation_start);
print "\n==> Validation completed in $d day(s), $h hour(s), $m minute(s) and $s second(s))\n";
